apply plugin: 'java'
apply plugin: 'maven'

group = "org.zeromq"
version = "0.1.0-SNAPSHOT"

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile 'org.zeromq:czmq-jni:0.1.0-SNAPSHOT'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

task generateJniHeaders(type: Exec, dependsOn: 'classes') {
    println "Generate JNI Headers"

    def classpath = sourceSets.main.output.classesDir
    def appclasspath = configurations.runtime.files*.getAbsolutePath().join(":")
    def nativeIncludes = "src/native/include"
    def jniClasses = [
        'org.zeromq.zyre.Zyre',
        'org.zeromq.zyre.ZyreEvent'
    ]
    commandLine("javah", "-d", "$nativeIncludes", "-classpath", "$classpath:$appclasspath", *jniClasses)
}

tasks.withType(Test) {
    systemProperty "java.library.path", "/usr/lib:/usr/local/lib:$projectDir"
}

task initCMake(type: Exec, dependsOn: 'generateJniHeaders') {
    commandLine "cmake", "."
}

task buildNative(type: Exec, dependsOn: 'initCMake') {
    commandLine "make"
}

jar.dependsOn buildNative
test.dependsOn buildNative
